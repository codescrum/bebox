node default {
  include 'rbenv'
  include 'nginx'
  include 'wkhtmltopdf'
  include 'imagemagick'
  include 'htop'
  include 'postfix'
  include 'install_redis'
  include 'install_mysql'
  include 'install_postgresql'
  include 'install_mongodb'
  include 'install_newrelic'
}

class rbenv {
  # Obtain and install rbenv from hiera
  $rbenv_install_hash = { 'rbenv_install' => hiera('rbenv::install', {}) }
  create_resources('rbenv::install', $rbenv_install_hash)
  # Obtain and compile ruby versions from hiera
  $rbenv_compile_hash = hiera('rbenv::compile', {})
  create_resources('rbenv::compile', $rbenv_compile_hash)
  # Obtain and install gems from hiera
  $rbenv_gem_hash = hiera('rbenv::gem', {})
  create_resources('rbenv::gem', $rbenv_gem_hash)
}

class wkhtmltopdf {
  package { 'wkhtmltopdf':
    ensure => present
  }
}

class imagemagick {
  package { 'imagemagick':
    ensure => present
  }
}

class htop{
  package { "htop":
    ensure => "present",
  }
}

class install_redis {
  # Obtain redis install parameters from hiera
  $redis_install = hiera('redis::install', {})
  $redis_version = $redis_install[redis_version]
  $redis_build_dir = $redis_install[redis_build_dir]
  $redis_install_dir = $redis_install[redis_install_dir]
  # Instance the redis install class
  class { 'redis::install':
    redis_version     => $redis_version,
    redis_build_dir   => $redis_build_dir,
    redis_install_dir => $redis_install_dir
  }
  # Obtain and create redis server instances from hiera
  $redis_servers_hash = hiera('redis::server', {})
  create_resources('redis::server', $redis_servers_hash)
}

class install_mysql{
  # Obtain, install and configure mysql server from hiera
  $mysql_server_hash = hiera('mysql::server', {})
  $root_password = $mysql_server_hash[root_password]
  $override_options = $mysql_server_hash[override_options]
  class { 'mysql::server':
    root_password => $root_password,
    override_options => $override_options
  }
}

class install_postgresql{
  # Obtain postgresql server data from hiera
  $postgresql_server_hash = hiera('postgresql::server', {})
  $listen = $postgresql_server_hash[listen]
  $port = $postgresql_server_hash[port]
  $version = $postgresql_server_hash[version]

  # Add apt source list from postgresql.org - Updated packages
  anchor { 'install_postgresql::install::begin': }
  include 'install_postgresql::apt'

  class { 'postgresql::server':
    listen => $listen,
    port => $port,
    version => $version,
    require => [
      Anchor['install_postgresql::install::begin'],
      Class['install_postgresql::apt']
    ]
  }
}

class install_postgresql::apt {
  apt::source{
    'apt.postgresql.org':
      location    => 'http://apt.postgresql.org/pub/repos/apt/',
      release     => "${::lsbdistcodename}-pgdg",
      repos       => "main ${version}",
      key         => 'ACCC4CF8',
      key_source  => 'http://apt.postgresql.org/pub/repos/apt/ACCC4CF8.asc',
      include_src => false,
  }
}

class install_mongodb{
  include mongodb
  # Obtain, install and configure mongodb server instances from hiera
  $mongodb_instances_hash = hiera('mongodb::mongod', {})
  create_resources('mongodb::mongod', $mongodb_instances_hash)
}

class install_newrelic {
  # Obtain and install newrelic from hiera
  $newrelic_hash = hiera('newrelic', {})
  $license_key = $newrelic_hash[license_key]
  $use_latest = $newrelic_hash[use_latest]
  class { 'newrelic':
    license_key => $license_key,
    use_latest  => $use_latest
  }
}